

module Compiler.ClosureIR where

open import Base.Prelude

-----------------------------------------------------
-- The base types
data BaseTy : ð’°â‚€ where
  NN : BaseTy -- "natural numbers"


-----------------------------------------------------
-- A simple, typed lambda calculus

-- The types:
data Ty : ð’°â‚€ where
  Î¹ : BaseTy -> Ty
  _â‡’_ : Ty -> Ty -> Ty

Ctx = List Ty

-- The terms:
-- We allow only well typed terms, the constructors mirror
-- the typing rules for LC
data _âŠ¢_ : Ctx -> Ty -> ð’°â‚€ where
  app : âˆ€{Î“ A B} -> Î“ âŠ¢ A â‡’ B -> Î“ âŠ¢ A -> Î“ âŠ¢ B
  lam : âˆ€{Î“ A B} -> Î“ ,, A âŠ¢ B -> Î“ âŠ¢ A â‡’ B
  var : âˆ€{Î“ A} -> A âˆˆ Î“ -> Î“ âŠ¢ A

infixr 50 _âŠ¢_
infixr 70 _â‡’_

-----------------------------------------------------
-- Another simple, typed lambda calculus
-- (but here, we use a different representation of types)


-- The types:
data UncurTy : ð’°â‚€
-- They are represented as "uncurried" types, ie: they only ever have base types as targets
-- E.g.:
-- The type (NN â‡’ (NN â‡’ NN))
-- is now written as: ((([] â‡‰ NN) :: ([] â‡‰ NN) :: []) â‡‰ NN)

-- The contexts:
UncurCtx = List UncurTy

-- The actual definition of types:
data UncurTy where
  _â‡‰_ : UncurCtx -> BaseTy -> UncurTy

infixl 40 _â‡‰_

-- The terms:
infixr 50 _âŠ¢uc_
data _âŠ¢uc_ : UncurCtx -> UncurTy -> ð’°â‚€ where
  app : âˆ€{Î“ A Bs B} -> Î“ âŠ¢uc ((A :: Bs) â‡‰ B) -> Î“ âŠ¢uc A -> Î“ âŠ¢uc (Bs â‡‰ B)
  lam : âˆ€{Î“ A Bs B} -> Î“ ,, A âŠ¢uc (Bs â‡‰ B) -> Î“ âŠ¢uc ((A :: Bs) â‡‰ B)
  var : âˆ€{Î“ A} -> A âˆˆ Î“ -> Î“ âŠ¢uc A


normalâ†’uc-Ctx : Ctx -> UncurCtx
normalâ†’uc-Ty : Ty -> UncurTy

normalâ†’uc-Ctx = map-List normalâ†’uc-Ty

normalâ†’uc-Ty (Î¹ x) = [] â‡‰ x
normalâ†’uc-Ty (A â‡’ B) with normalâ†’uc-Ty B
... | (Xs â‡‰ X) = (normalâ†’uc-Ty A :: Xs) â‡‰ X

-- TODO: Create a function from usual lambda terms to UC lambda terms
-- normalâ†’uc-Te : âˆ€{Î“ A} -> Î“ âŠ¢ A -> normalâ†’uc-Ctx Î“ âŠ¢uc normalâ†’uc-Ty A
-- normalâ†’uc-Te (app t s) = {!!}
-- normalâ†’uc-Te (lam t) = {!!}
-- normalâ†’uc-Te (var x) = {!!}


-----------------------------------------------------
-- The "Closure Intermediate Representation"

-- We now differentiate between computed and not yet computed values
-- For this, we define a new set of types (Cl[osure] Ty[pes]):
data ClTy : ð’°â‚€
ClCtx = List ClTy

-- But inside of these, we also use types, which are just like UncurTy
-- TODO: Is this really necessary?
data ClUncurTy : ð’°â‚€
data ClUncurTy where
  _â‡‰_ : ClCtx -> BaseTy -> ClUncurTy

-- The actual Closure Types:
data ClTy where
  Î¹ : BaseTy -> ClTy
  Closure : ClUncurTy -> ClTy


-- A term in this IR is given by a sequence of commands, which, when given input data
-- of types as described in context Î“, produces data as required by context Î”
data CommandSeq : (Î“ : ClCtx) -> (Î” : ClCtx) -> ð’°â‚€

-- A command takes data in a context Î“, and produces a value of type A
data Command : (Î“ : ClCtx) -> (A : ClTy) -> ð’°â‚€

-- A function, similarly, produces a value in a context. This datatype currently only
-- is there for being more explicit about what is meant
data Function : ClCtx -> ClTy -> ð’°â‚€

-- Def of a sequence of commands
data CommandSeq where
  -- an empty sequence:
  init : âˆ€{Î“} -> CommandSeq Î“ Î“
  -- given a sequence, add a new value to the produced context, by executing a single command
  _&_ : âˆ€{Î“ Î” A} -> CommandSeq Î“ Î” -> Command Î” A -> CommandSeq Î“ (Î” ,, A)

infixl 20 _&_

-- Def of command
data Command where
  -- If we have a value in our context, we can get that again as value
  proj : âˆ€{Î“ A} -> A âˆˆ Î“ -> Command Î“ A

  -- Given a function ("definition"), we can create a closure, and take that as a value
  -- The closure, at first, has no "closured data"
  makeClosure : âˆ€{Î“ Î” A} -> Function Î“ (Î¹ A) -> Command Î” (Closure (Î“ â‡‰ A))

  -- If we can produce a value (: A) which is needed by a closure, we can write it into it,
  -- producing a closure which needs less arguments
  write2 : âˆ€{Î“ As A As2 B} -> A âˆˆ Î“ -> (Closure ((As ++ (([] ,, A) ++ As2)) â‡‰ B)) âˆˆ Î“ -> Command Î“ (Closure (As ++ As2 â‡‰ B))

  -- if we have a closure, for which all arguments are given, we can execute it, to produce a
  -- value of its return type
  call : âˆ€{Î“ A} -> (Closure ([] â‡‰ A)) âˆˆ Î“ -> Command Î“ (Î¹ A)

  -- Execute a sequence of commands, and get the last produced value
  inline : âˆ€{Î“ Î” A} -> CommandSeq Î“ (Î” ,, A) -> Command Î“ A

  -- These are probably not strictly necessary, but the make writing chains of commands easier.
  -- If a command needs data from a context Î“, we can also execute it in an extended context Î“ ++ Î”
  weak : âˆ€{Î” Î“ A} -> Command Î“ A -> Command (Î“ ++ Î”) A
  weak1 : âˆ€{B Î“ A} -> Command (Î“) A -> Command (Î“ ,, B) A

-- A function is merely a seqence of commands
data Function where
  ret : âˆ€{Î“ Î” A} -> CommandSeq Î“ (Î” ,, A) -> Function Î“ A


--------------------------------------------------------------------------------
-- Compiling the âŠ¢uc ("uncurried-type") terms to the ClosureIR
--------------------------------------------------------------------------------

-------------------------------------------------
-- Mapping the types

-- We need to map the uc types to cl types, as well as the contexts
ucâ†’cl-Ctx : UncurCtx -> ClCtx

ucâ†’cl : UncurTy -> ClTy
ucâ†’cl (A â‡‰ B) = Closure (ucâ†’cl-Ctx A â‡‰ B)

ucâ†’cl-Ctx [] = []
ucâ†’cl-Ctx (A :: As) = ucâ†’cl A :: ucâ†’cl-Ctx As

ucâ†’cl-âˆˆ : âˆ€{A Î“} -> A âˆˆ Î“ -> ucâ†’cl A âˆˆ ucâ†’cl-Ctx Î“
ucâ†’cl-âˆˆ {x â‡‰ xâ‚} {.(x â‡‰ xâ‚) :: G} this = this
ucâ†’cl-âˆˆ {x â‡‰ xâ‚} {xâ‚‚ :: G} (next i) = next (ucâ†’cl-âˆˆ i)


-------------------------------------------------
-- Additional command shortcuts

-- We define some shortcuts for the "write2" command
write : âˆ€{Î“ A As B} -> A âˆˆ Î“ -> (Closure ((As ,, A) â‡‰ B)) âˆˆ Î“ -> Command Î“ (Closure (As â‡‰ B))
write {Î“} {A} {As} {B} i c = write2 {As = []} {A = A} {As2 = As} {B = B} i c

-- Also, for writing more values at once
writeCtx2 : âˆ€{Î“â‚€ Î“ Î“â‚ Î” A} -> Closure (Î“â‚€ ++ (Î“ ++ Î“â‚) â‡‰ A) âˆˆ Î” -> Î“ âŠ† Î” -> Command Î” (Closure ((Î“â‚€ ++ Î“â‚) â‡‰ A))
writeCtx2 {G0} {[]} {G1} {D} c g = proj c
writeCtx2 {G0} {x :: G} {G1} {D} {A} c g =
  let qq = write2 {Î“ = D} {As = G0} {A = x} {As2 = (G ++ G1)} {B = A} (trans-âˆˆ-âŠ† this g) c
      p = init {Î“ = D}
          & qq
          & writeCtx2 {Î“â‚€ = G0} {Î“ = G} {Î“â‚ = G1} this (trans-âŠ† (skip refl) (trans-âŠ† g (skip refl)))
  in inline p

-- Here as well
writeCtx : âˆ€{Î“â‚€ Î“ Îž A} -> Closure (Îž â‡‰ A) âˆˆ Î“â‚€ -> Command (Î“â‚€ ++ (Î“ ++ Îž)) (Closure ([] â‡‰ A))
writeCtx {Î“â‚€} {Î“} {Îž} {A} c with writeCtx2 {Î“â‚€ = []} {Î“ = Îž} {Î“â‚ = []} {Î” = Î“â‚€ ++ (Î“ ++ Îž)}
... | ff rewrite List-++-[] Îž = ff (trans-âˆˆ-âŠ† c (++â†’âŠ†-r _)) (trans-âŠ† (++â†’âŠ†-l Î“) (++â†’âŠ†-l Î“â‚€))


-------------------------------------------------
-- The actual translation/compilation

-- We have to versions of the compilation function

-- The first, compiles a term into a command for producing exactly a value of type A in a context Î“
compileCommand : âˆ€{Î“ A} -> (t : Î“ âŠ¢uc (A)) -> Command (ucâ†’cl-Ctx Î“) (ucâ†’cl A)

-- The second, compiles a term into a function definition. But without regards to the actual
-- type of the term, it compiles it into a function with an as much as possible uncurried type.
-- I.e., a term [â„•] âŠ¢ â„• â‡’ â„• is compiled to [â„• , â„•] âŠ¢ â„•
compileFunction : âˆ€{Î“ As A} -> (t : Î“ âŠ¢uc (As â‡‰ A)) -> Function (ucâ†’cl-Ctx Î“ ++ ucâ†’cl-Ctx As) (Î¹ A)


-- The def
-- A term is translated into a sequence of commands (each of which adds a new value to the context,
-- and the last is taken as result value)
compileCommand {Î“} {(_ â‡‰ _)} (app t s) =
  let p = init {Î“ = ucâ†’cl-Ctx Î“}
          & compileCommand t
          & weak1 (compileCommand s)
          & write this (next this)
  in inline p
compileCommand {Î“} {.((_ :: _) â‡‰ _)} (lam t) =
  let p = init {Î“ = ucâ†’cl-Ctx (Î“)}
           & makeClosure (compileFunction t)
           & writeCtx2 {Î“â‚€ = [] ,, _} this (skip refl)

  in inline p
compileCommand {Î“} {A} (var x) = proj (ucâ†’cl-âˆˆ x)


-- The def
compileFunction {Î“} {As} (app {A = (Xs â‡‰ X)} t s) =
  let p = init {ucâ†’cl-Ctx Î“ ++ ucâ†’cl-Ctx As}
          & weak (compileCommand t)
          & weak1 (weak (compileCommand s))
          & write (this) (next this)
          & writeCtx {Î“â‚€ = _ :: _ :: _ :: []} {Î“ = ucâ†’cl-Ctx Î“} {Îž = ucâ†’cl-Ctx As} this
          & call this
  in ret p
compileFunction {Î“} {As ,, A} (lam {B = B} t) =
  let t2 = compileFunction t
      p = init {ucâ†’cl-Ctx Î“ ++ (ucâ†’cl A :: ucâ†’cl-Ctx As)}
          & makeClosure (compileFunction t)
          & writeCtx2 {Î“â‚€ = [] ,, ucâ†’cl A} {Î“ = ucâ†’cl-Ctx Î“} {Î“â‚ = ucâ†’cl-Ctx As} this (skip (++â†’âŠ†-r _))
          & writeCtx {Î“â‚€ = _ :: _ :: []} this
          & call this

  in ret p
compileFunction {Î“} {As}(var x) =
  let p = init {Î“ = ucâ†’cl-Ctx Î“ ++ ucâ†’cl-Ctx As}
          & proj (trans-âˆˆ-âŠ† (ucâ†’cl-âˆˆ x) (++â†’âŠ†-r _))
          & writeCtx {Î“â‚€ = _ :: []} this
          & call this
  in ret p


--------------------------------------------------------------------------------
-- Interpreting the ClosureIR back into Agda terms
--------------------------------------------------------------------------------

-- Interpretation of Contexts
CtxInt : ClCtx -> ð’°â‚€

-- Interpretation of base types
BaseTyInt : BaseTy -> ð’°â‚€
BaseTyInt (NN) = â„•

-- Interpretation of closure types
ClTyInt : ClTy -> ð’°â‚€
ClTyInt (Î¹ a) = BaseTyInt a
ClTyInt (Closure (x â‡‰ y)) = CtxInt x -> BaseTyInt y

CtxInt [] = âŠ¤
CtxInt (x :: G) = ClTyInt x Ã— CtxInt G


-- Interpretation of the `proj` command
projInt : âˆ€{Î“ A} -> A âˆˆ Î“ -> CtxInt Î“ -> ClTyInt A
projInt this (x , _) = x
projInt (next i) (_ , e) = projInt i e

-- Interpretation of a command
-- A (Command Î“ A) is interpreted into a function from âŸ¦ Î“ âŸ§ to âŸ¦ A âŸ§
eval : âˆ€{Î“ A} -> Command Î“ A -> CtxInt Î“ -> ClTyInt A

-- Interpretation of a command sequence
evalSeq : âˆ€{Î“ Î”} -> CommandSeq Î“ Î” -> CtxInt Î“ -> CtxInt Î”
evalSeq init x = x
evalSeq (s & x) e = let sv = evalSeq s e
                        xv = eval x sv
                    in xv , sv

evalFun : âˆ€{Î“ A} -> Function Î“ A -> CtxInt Î“ -> ClTyInt A
evalFun (ret x) e = let (r , _) = evalSeq x e
                    in r

-- Interpreting `weak`
evalWeak : âˆ€{Î“ Î”} -> CtxInt (Î“ ++ Î”) -> CtxInt Î“
evalWeak {[]} {D} e = tt
evalWeak {x :: G} {D} (v1 , vr) = v1 , evalWeak vr

-- Interpreting `write`
splice : âˆ€{Î“ A Î”} -> CtxInt (Î“ ++ Î”) -> ClTyInt A -> CtxInt (Î“ ++ (A :: Î”))
splice {[]} {A} {D} e a = a , e
splice {G0 :: G} {A} {D} (x , e) a = x , splice e a

eval (call x) e = projInt x e tt
eval (makeClosure f) _ e2 = evalFun f e2
eval (weak {Î” = D} {Î“ = G} v) e = eval v (evalWeak e)
eval (weak1 v) (_ , e) = eval v e
eval (inline x) e = evalSeq x e .fst
eval (write2 t s) e e2 =
  let vt = projInt t e
      vs = projInt s e
  in vs (splice e2 vt)
eval (proj x) e = projInt x e



--------------------------------------------------------------------------------
-- Testing
--------------------------------------------------------------------------------

module Test where
  -- This is: Î» y -> (Î» x -> x) y
  t : [] âŠ¢uc (([] ,, ([] â‡‰ NN)) â‡‰ NN)
  t = lam (app (lam (var this)) (var this))

  te = compileCommand t
  tee = eval te -- Normalizes to: Î» e e2 â†’ fst e2 tt

  pro-1 pro-2 : [] âŠ¢uc (([] ,, ([] â‡‰ NN) ,, ([] â‡‰ NN)) â‡‰ NN)
  pro-1 = lam (lam (var this))        -- Î» x y -> y
  pro-2 = lam (lam (var (next this))) -- Î» x y -> x

  pro-1-c = compileFunction pro-1
  pro-1-e = evalFun pro-1-c -- Normalizes to: Î» e â†’ fst (snd e) tt

  pro-2-c = compileFunction pro-2
  pro-2-e = evalFun pro-2-c -- Normalizes to: Î» e â†’ fst e tt


